-------------------
Sobre o exercício:
- O método de ordenação Insert Sort retira um elemento do vetor e procura a posição correta dele
- Sempre esse método vai começar a verificação do índice i + 1
- Vai ser criado uma cópia do elemento do índice que está sendo comparado com o índice atual - 1
- Vai ser criado uma variável que vai armazenar a posição do índice do elemento que estamos comparando 
-------------------

Algoritmo "Insert_Sort"

Var
   i, copy, index: Inteiro
   array: vetor[0..29] de Inteiro

Inicio
   Escreva("Preencha um vetor de 30 posições com valores ímpares!")
   Escreval()
   Para i <- 0 ate 29 faca
        Escreva("Posição",i,", informe o elemento: ")
        Leia(array[i])
   Fimpara
   
   Para i <- 1 ate 29 faca   // Laço de repetição externo
        copy <- array[i]
        index <- i
        Enquanto (index > 0) E (array[index - 1] > copy) faca // Laço de repetição interno
                 array[index] <- array[index - 1]
                 index <- index - 1
                 Se index = 0 entao
                    Interrompa
                 Fimse
        Fimenquanto
        array[index] <- copy
   Fimpara
   
   Escreval()
   Escreval("Aplicando o método de ordenação Insert Sort:")
   Escreval()
   
   Para i <- 0 ate 29 faca
        Escreva(array[i])
   Fimpara
Fimalgoritmo
